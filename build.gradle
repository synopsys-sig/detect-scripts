buildscript {
    apply from: 'https://raw.githubusercontent.com/blackducksoftware/integration-resources/6b0f809c99b4689dcf24f28e57c4d8664a8883db/gradle_common/buildscript-repositories.gradle', to: buildscript
    apply from: 'https://raw.githubusercontent.com/blackducksoftware/integration-resources/master/gradle_common/buildscript-cgp-version.gradle'

    ext {
        javaTargetCompatibility = 8
        javaSourceCompatibility = 8
    }

    dependencies {
        classpath "com.synopsys.integration:common-gradle-plugin:${managedCgpVersion}"
        classpath 'com.synopsys.integration:integration-rest:10.3.3'
        classpath 'org.codehaus.groovy:groovy-all:2.4.12'
        classpath 'org.junit.jupiter:junit-jupiter:5.4.2'
        classpath 'org.mockito:mockito-core:2.+'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '5.0.0'
}

def internalRepoHost = System.getenv('SNPS_INTERNAL_ARTIFACTORY')

apply from: 'https://raw.githubusercontent.com/blackducksoftware/integration-resources/master/gradle_common/buildscript-repositories.gradle'

repositories {
    maven { url "${internalRepoHost}/artifactory/jcenter" }
}

group = 'com.synopsys.integration'
version = '3.2.0-SNAPSHOT'

apply plugin: 'com.synopsys.integration.simple'

jar {
    manifest {
        attributes 'Main-Class': 'com.synopsys.integration.synopsys.detect.scripts.Application'
    }
}

shadowJar {
    getArchiveBaseName().set('synopsys-detect-scripts')
    classifier = null
}

jar.finalizedBy shadowJar

build.dependsOn.add("generateVersionFile")
build.dependsOn.add("generateScripts")
build.dependsOn.add("generateCert")

task generateVersionFile {
    final File versionFile = new File(project.projectDir, "src/main/resources/version.txt")
    versionFile.delete()
    versionFile << version
}

task generateScripts(type: JavaExec) {
    main = "-jar"
    args(jar.getArchiveFile().get().asFile, "scripts", "${project.buildDir}/libs/")
}

task generateCert(type: JavaExec) {
    main = "-jar"
    args(jar.getArchiveFile().get().asFile, "cert", "${project.buildDir}/libs/")
}

dependencies {
    implementation 'com.synopsys.integration:blackduck-common:62.0.1'
    implementation 'org.freemarker:freemarker:2.3.31'
}
